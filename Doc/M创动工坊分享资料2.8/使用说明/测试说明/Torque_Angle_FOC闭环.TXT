//灯哥开源，遵循GNU协议，转载请著名版权！
//GNU开源协议（GNU General Public License, GPL）是一种自由软件许可协议，保障用户能够自由地使用、研究、分享和修改软件。
//该协议的主要特点是，要求任何修改或衍生的作品必须以相同的方式公开发布，即必须开源。此外，该协议也要求在使用或分发软件时，必须保留版权信息和许可协议。GNU开源协议是自由软件基金会（FSF）制定和维护的一种协议，常用于GNU计划的软件和其他自由软件中。
//感谢灯哥开源
//程序代码“M创动工坊”测试确认可行，硬件：ESP32 Lolin D32，SimpleFOC mini驱动板，2804直流无刷电机，AS5600磁编码器，12V2A直流电源。
//软件平台：开发板版本ESP32 by Espressif Systems 2.0.4，在开发板管理器中注意选择

#include <Arduino.h>
#include "AS5600.h"

#define _constrain(amt, low, high) ((amt) < (low) ? (low) : ((amt) > (high) ? (high) : (amt)))
float voltage_power_supply;
float Ualpha, Ubeta = 0, Ua = 0, Ub = 0, Uc = 0;
#define _3PI_2 4.71238898038f
float zero_electric_angle = 0;
int PP = 1, DIR = 1;//方向参数与编码器方向参数需要根据实际情况修改
//驱动板与MCU的PWM引脚定义
int pwmA = 32;
int pwmB = 33;
int pwmC = 25;
int EN1 = 12;//MS8313使能引脚定义

int Sensor_DIR = -1;  //传感器方向，需要调试，也可能是1
int Motor_PP = 7;     //电机极对数

//初始变量及函数定义
#define _constrain(amt, low, high) ((amt) < (low) ? (low) : ((amt) > (high) ? (high) : (amt)))
//宏定义实现的一个约束函数,用于限制一个值的范围。
//具体来说，该宏定义的名称为 _constrain，接受三个参数 amt、low 和 high，分别表示要限制的值、最小值和最大值。该宏定义的实现使用了三元运算符，根据 amt 是否小于 low 或大于 high，返回其中的最大或最小值，或者返回原值。
//换句话说，如果 amt 小于 low，则返回 low；如果 amt 大于 high，则返回 high；否则返回 amt。这样，_constrain(amt, low, high) 就会将 amt 约束在 [low, high] 的范围内。1


// 归一化角度到 [0,2PI]
float _normalizeAngle(float angle) {
  float a = fmod(angle, 2 * PI);  //取余运算可以用于归一化，列出特殊值例子算便知
  return a >= 0 ? a : (a + 2 * PI);
  //三目运算符。格式：condition ? expr1 : expr2
  //其中，condition 是要求值的条件表达式，如果条件成立，则返回 expr1 的值，否则返回 expr2 的值。可以将三目运算符视为 if-else 语句的简化形式。
  //fmod 函数的余数的符号与除数相同。因此，当 angle 的值为负数时，余数的符号将与 _2PI 的符号相反。也就是说，如果 angle 的值小于 0 且 _2PI 的值为正数，则 fmod(angle, _2PI) 的余数将为负数。
  //例如，当 angle 的值为 -PI/2，_2PI 的值为 2PI 时，fmod(angle, _2PI) 将返回一个负数。在这种情况下，可以通过将负数的余数加上 _2PI 来将角度归一化到 [0, 2PI] 的范围内，以确保角度的值始终为正数。
}


// 设置PWM到控制器输出
void setPwm(float Ua0, float Ub0, float Uc0) {

  // 计算占空比
  // 限制占空比从0到1
  float dc_a = _constrain(Ua0 / voltage_power_supply, 0.0f, 1.0f);
  float dc_b = _constrain(Ub0 / voltage_power_supply, 0.0f, 1.0f);
  float dc_c = _constrain(Uc0 / voltage_power_supply, 0.0f, 1.0f);

  //写入PWM到PWM 0 1 2 通道
  ledcWrite(0, dc_a * 255);
  ledcWrite(1, dc_b * 255);
  ledcWrite(2, dc_c * 255);
}

void setTorque(float Uq, float angle_el) {
  Uq = _constrain(Uq, -voltage_power_supply / 2, voltage_power_supply / 2);
  float Ud = 0;
  angle_el = _normalizeAngle(angle_el);
  // 帕克逆变换
  Ualpha = -Uq * sin(angle_el);
  Ubeta = Uq * cos(angle_el);

  // 克拉克逆变换
  Ua = Ualpha + voltage_power_supply / 2;
  Ub = (sqrt(3) * Ubeta - Ualpha) / 2 + voltage_power_supply / 2;
  Uc = (-Ualpha - sqrt(3) * Ubeta) / 2 + voltage_power_supply / 2;
  setPwm(Ua, Ub, Uc);
}

void DFOC_Vbus(float power_supply) {
  voltage_power_supply = power_supply;
  pinMode(pwmA, OUTPUT);
  pinMode(pwmB, OUTPUT);
  pinMode(pwmC, OUTPUT);
  ledcSetup(0, 30000, 8);  //pwm频道, 频率, 精度
  ledcSetup(1, 30000, 8);  //pwm频道, 频率, 精度
  ledcSetup(2, 30000, 8);  //pwm频道, 频率, 精度
  ledcAttachPin(pwmA, 0);
  ledcAttachPin(pwmB, 1);
  ledcAttachPin(pwmC, 2);
  Serial.println("完成PWM初始化设置");
  BeginSensor();
}


float _electricalAngle() {
  return _normalizeAngle((float)(DIR * PP) * getAngle_Without_track() - zero_electric_angle);
}


void DFOC_alignSensor(int _PP, int _DIR) {
  PP = _PP;
  DIR = _DIR;
  setTorque(3, _3PI_2);
  delay(3000);
  zero_electric_angle = _electricalAngle();
  setTorque(0, _3PI_2);
  Serial.print("0电角度：");
  Serial.println(zero_electric_angle);
}

float DFOC_M0_Angle() {
  return getAngle();
}

//前面代码为FOC核心代码

//==============串口接收==============
float motor_target;
int commaPosition;
String serialReceiveUserCommand() {

  // a string to hold incoming data
  static String received_chars;

  String command = "";

  while (Serial.available()) {
    // get the new byte:
    char inChar = (char)Serial.read();
    // add it to the string buffer:
    received_chars += inChar;

    // end of user input
    if (inChar == '\n') {

      // execute the user command
      command = received_chars;

      commaPosition = command.indexOf('\n');  //检测字符串中的逗号
      if (commaPosition != -1)                //如果有逗号存在就向下执行
      {
        motor_target = command.substring(0, commaPosition).toDouble();  //电机角度
        Serial.println(motor_target);
      }
      // reset the command buffer
      received_chars = "";
    }
  }
  return command;
}

float serial_motor_target() {
  return motor_target;
}



void setup() {
  Serial.begin(115200);
  DFOC_Vbus(12);  //设定驱动器供电电压
  // 必须先开使能，由M创动工坊追加，simplefoc mini板的en引脚接12号引脚，
  pinMode(EN1, OUTPUT);
  digitalWrite(EN1, HIGH);
  DFOC_alignSensor(Motor_PP, Sensor_DIR);
}

void loop() {
  //输出角度值
  //Serial.print("当前角度：");
  //Serial.println(DFOC_M0_Angle());
  //输出角度值
  float Kp = 0.01;
  float Sensor_Angle = DFOC_M0_Angle();
  setTorque(Kp * (serial_motor_target() - Sensor_DIR * Sensor_Angle) * 180 / PI, _electricalAngle());  //位置闭环
  serialReceiveUserCommand();
}


//下面的代码是单独测试编码器的，和上面的是互斥的，所以注释掉
// #include "DengFOC.h"

// void setup() {
//   Serial.begin(115200);  //启动串口发送接收
//   DFOC_Vbus(12);   //设定驱动器供电电压
// }

// void loop()
// {
//   //输出角度值
//   Serial.print("当前角度：");
//   Serial.println(DFOC_M0_Angle());  //调用DFOC_M0_Angle()函数打印出角度值
// }
